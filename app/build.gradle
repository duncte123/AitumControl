plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'me.duncte123.aitumcontrol'
    compileSdk 35

    def versionPropsFile = file('version.properties')

    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        int versionBuild = versionProps['VERSION_BUILD'].toInteger()

        defaultConfig {
            applicationId "me.duncte123.aitumcontrol"
            minSdk 24
            targetSdk 35
            versionCode versionBuild
            versionName "1.7.${versionCode}"

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }
    } else {
        throw new FileNotFoundException("Could not read version.properties!")
    }

    /*Wrapping inside a method avoids auto incrementing on every gradle task run. Now it runs only when we build apk*/
    ext.autoIncrementBuildNumber = {
        if (versionPropsFile.canRead()) {
            Properties versionProps = new Properties()
            versionProps.load(new FileInputStream(versionPropsFile))
            int versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
            versionProps['VERSION_BUILD'] = versionBuild.toString()
            versionProps.store(versionPropsFile.newWriter(), null)
        } else {
            throw new FileNotFoundException("Could not read version.properties!")
        }
    }

    buildTypes {
        debug {
            // TODO: make properties dynamic?
            def debugPropsFile = file('debug_options.properties')

            if (debugPropsFile.canRead()) {
                Properties versionProps = new Properties()
                versionProps.load(new FileInputStream(debugPropsFile))
                def useTestRules = versionProps['USE_DEBUG_RULES']
                buildConfigField "boolean", "USE_FAKE_RULES", "Boolean.parseBoolean(\"$useTestRules\")" // why google :(
            } else {
                buildConfigField "boolean", "USE_FAKE_RULES", 'Boolean.parseBoolean("false")'
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "USE_FAKE_RULES", 'Boolean.parseBoolean("false")' // why google :(
        }
    }

    // Hook to check if the release/debug task is among the tasks to be executed.
    //Let's make use of it
    gradle.taskGraph.whenReady { taskGraph ->
        if (taskGraph.hasTask(assembleDebug)) {  // when running debug task
            autoIncrementBuildNumber()
        } else if (taskGraph.hasTask(assembleRelease) || taskGraph.hasTask(bundleRelease)) { /* when running release task */
            autoIncrementBuildNumber()
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'

    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.preference:preference:1.2.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}